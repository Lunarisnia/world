in vec2 vUv;
in vec3 vNormal;
in vec3 vFragPos;
in mat4 vModelMatrix;

uniform float uTime;
uniform vec3 uCameraPosition;

// Source: https://www.shadertoy.com/view/4dffRH
vec3 hash(vec3 p) // this hash is not production ready, please
{ // replace this by something better
    p = vec3(dot(p, vec3(127.1, 311.7, 74.7)),
            dot(p, vec3(269.5, 183.3, 246.1)),
            dot(p, vec3(113.5, 271.9, 124.6)));

    return -1.0 + 2.0 * fract(sin(p) * 43758.5453123);
}

float perlinNoise(in vec3 x)
{
    // grid
    vec3 p = floor(x);
    vec3 w = fract(x);

    // quintic interpolant
    vec3 u = w * w * w * (w * (w * 6.0 - 15.0) + 10.0);

    // gradients
    vec3 ga = hash(p + vec3(0.0, 0.0, 0.0));
    vec3 gb = hash(p + vec3(1.0, 0.0, 0.0));
    vec3 gc = hash(p + vec3(0.0, 1.0, 0.0));
    vec3 gd = hash(p + vec3(1.0, 1.0, 0.0));
    vec3 ge = hash(p + vec3(0.0, 0.0, 1.0));
    vec3 gf = hash(p + vec3(1.0, 0.0, 1.0));
    vec3 gg = hash(p + vec3(0.0, 1.0, 1.0));
    vec3 gh = hash(p + vec3(1.0, 1.0, 1.0));

    // projections
    float va = dot(ga, w - vec3(0.0, 0.0, 0.0));
    float vb = dot(gb, w - vec3(1.0, 0.0, 0.0));
    float vc = dot(gc, w - vec3(0.0, 1.0, 0.0));
    float vd = dot(gd, w - vec3(1.0, 1.0, 0.0));
    float ve = dot(ge, w - vec3(0.0, 0.0, 1.0));
    float vf = dot(gf, w - vec3(1.0, 0.0, 1.0));
    float vg = dot(gg, w - vec3(0.0, 1.0, 1.0));
    float vh = dot(gh, w - vec3(1.0, 1.0, 1.0));

    // interpolation
    return va +
        u.x * (vb - va) +
        u.y * (vc - va) +
        u.z * (ve - va) +
        u.x * u.y * (va - vb - vc + vd) +
        u.y * u.z * (va - vc - ve + vg) +
        u.z * u.x * (va - vb - ve + vf) +
        u.x * u.y * u.z * (-va + vb + vc - vd + ve - vf - vg + vh);
}

float fbm(vec3 position, int n, float persistence, float lacunarity) {
    float frequency = 1.0f;
    float amplitude = 0.5f;
    float total = 0.0f;
    float normalization = 0.0f;
    for (int i = 0; i < n; i++) {
        float noiseValue = perlinNoise(position * frequency);
        total += noiseValue * amplitude;
        normalization += amplitude;

        amplitude *= persistence;
        frequency *= lacunarity;
    }

    total /= normalization;

    return total;
}

float normalizeFBM(float n) {
    return (1.0f + n) / 2.0f;
}

void main() {
    float eps = 0.01f;
    int octaves = 2;
    float persistence = 0.5;
    float lacunarity = 2.0;

    vec3 pos = vec3(vFragPos) + vec3(cos(uTime), -sin(uTime), sin(uTime));
    float noiseSample = fbm(pos, octaves, persistence, lacunarity);
    noiseSample = (1.0f + noiseSample) / 2.0f;

    float d0 = normalizeFBM(fbm(pos, octaves, persistence, lacunarity));
    float dx = normalizeFBM(fbm(pos + vec3(eps, 0.0f, 0.0f), octaves, persistence, lacunarity));
    float dy = normalizeFBM(fbm(pos + vec3(0.0f, eps, 0.0f), octaves, persistence, lacunarity));
    float dz = normalizeFBM(fbm(pos + vec3(0.0f, 0.0f, eps), octaves, persistence, lacunarity));

    vec3 dNormal = vec3(
            (dx - d0) / eps,
            (dy - d0) / eps,
            (dz - d0) / eps
        );
    dNormal = normalize(vNormal - dNormal);

    vec3 lightPosition = vec3(0.0f, 0.0f, 4.0f) + vFragPos;

    vec3 lightDir = normalize(lightPosition - vFragPos);
    float diff = max(0.0f, dot(lightDir, dNormal));
    vec3 diffuse = vec3(vNormal) * diff;

    vec3 cameraDir = normalize(uCameraPosition - pos);
    vec3 halfDir = normalize(dNormal + cameraDir);
    float spec = pow(1.0f - max(0.0f, dot(halfDir, dNormal)), 0.22f);
    vec3 specular = vec3(1.0f) * spec;

    vec3 color = diffuse + specular;
    gl_FragColor = vec4(color, 1.0f);
}
